plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'dev.rosewood'
    version = '1.5.3'

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    }

    repositories {
        mavenCentral()

        maven { url = 'https://repo.bg-software.com/repository/api/' }
        maven { url = 'https://bitbucket.org/kangarko/libraries/raw/master' }
        maven { url = 'https://maven.enginehub.org/repo/' }
        maven { url = 'https://repo.md-5.net/content/repositories/public/' }
        maven { url = 'https://repo.codemc.org/repository/maven-public' }
        maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
        maven { url = 'https://mvn.lumine.io/repository/maven-public/' }
        maven { url = 'https://maven.playpro.com' }
        maven { url = 'https://repo.viaversion.com/' }
        maven { url = 'https://jitpack.io' }
        maven { url = 'https://libraries.minecraft.net' }
        maven { url = 'https://repo.papermc.io/repository/maven-public/' }
        maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url = 'https://repo.rosewooddev.io/repository/public/' }

        maven {
            url = 'https://repo.rosewooddev.io/repository/maven-public'
            credentials {
                username project.mavenUser
                password project.mavenPassword
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:23.1.0'
    }
}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS:Wrapper')
    implementation project(':NMS:v1_16_R3')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_18_R2', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R2', configuration: 'remapped')
    implementation project(':Hook:Hook-Wrapper')
    implementation project(':Hook:mcMMO-New')
    implementation project(':Hook:mcMMO-Old')
    implementation project(':Hook:MythicMobs-New')
    implementation project(':Hook:MythicMobs-Old')
}

shadowJar {
    archiveClassifier.set(null)

    relocate('dev.rosewood.rosegarden', 'dev.rosewood.rosestacker.lib.rosegarden')
    relocate('dev.rosewood.guiframework', 'dev.rosewood.rosestacker.lib.guiframework')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'rosestacker'
            pom {
                name = 'rosestacker'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
