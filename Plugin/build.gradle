import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
}

compileJava {
    options.compilerArgs += ['-parameters']
    options.fork = true
    options.forkOptions.executable = 'javac'
    options.encoding = 'UTF-8'
}

dependencies {
    // Other modules
    implementation project(':NMS-Wrapper')

    // Referenced dependencies
    compileOnly 'org.spigotmc:spigot:1.16.5'
    compileOnly 'net.coreprotect:coreprotect:19.4'
    compileOnly 'de.diddiz:logblock:1.16.1.1'
    compileOnly 'me.clip:placeholderapi:2.10.9'
    compileOnly 'org.jetbrains:annotations:16.0.2'
    compileOnly 'us.myles:viaversion:3.2.2-SNAPSHOT'
    compileOnly 'com.viaversion:viaversion:4.0.0-21w18a'

    // Hologram plugins
    compileOnly 'com.gmail.filoghost:holographicdisplays:2.4.3'
    compileOnly 'com.sainttx:holograms:2.13'
    compileOnly 'com.zrips:cmi:8.6.9.4'
    compileOnly 'me.arasple.mc:trhologram:2.2'

    // Stacker plugins
    compileOnly 'com.bgsoftware:wildstacker:3.1.1'
    compileOnly 'com.songoda:epicspawners:6.4'
    compileOnly 'com.songoda:ultimatestacker:2.0.8'
    compileOnly 'uk.antiperson:stackmob:5.2.2'

    // Misc other
    compileOnly 'com.songoda:epicbosses:1.2.12'
    compileOnly 'me.minebuilders:clearlag:3.1.6'
    compileOnly 'net.brcdev:shopgui:1.36.0'
    compileOnly 'net.citizensnpcs:citizens:2.0.27'
    compileOnly 'com.nisovin:shopkeepers:2.11.0'
    compileOnly 'io.lumine.xikage:mythicmobs:4.10.0'
    compileOnly 'com.magmaguy:elitemobs:7.2.16'
    compileOnly 'org.mineacademy.plugin:Boss-API:3.8.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.4'

    // Dependencies that will be shaded into the jar
    implementation 'dev.rosewood:rosegarden:1.0.7-SNAPSHOT'
    implementation 'dev.rosewood:guiframework:1.1.2-SNAPSHOT'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter ReplaceTokens, tokens: ["version": project.property("version")]
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }
}

shadowJar {
    archiveClassifier.set(null)
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'rosestacker'
            pom {
                name = 'rosestacker'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}
